# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
pkgname=docuum
pkgver=0.21.1
pkgrel=2
pkgdesc="LRU eviction of Docker images."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/stepchowfun/docuum"
license=('MIT')
makedepends=('cargo')
depends=('docker')
conflicts=("${pkgname}-git")
install="${pkgname}.install"
backup=("etc/default/${pkgname}")
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
        "${pkgname}.service")
sha512sums=('4bf4450b24bf264262c2ea87b347eaa5b3a679820a3e1982a05222eb574205a3055f9fe19a00f9214d1b42ee0b2d1ca9d066b1ae713fc4e42383e83059c5042a'
            '377e3368655e89184b96241c0e638ef64d7084da1ed23e02450c054740547667697147c9864844c25fd0fc11ec0c5eab30124de2e6d2c0466e038a59f191f6ff')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features -- --skip code_str_display
  # FIXME broken test skipped
}

package() {
  cd "${srcdir}"
  install -Dm0755 "${pkgname}-${pkgver}/target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm0644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
