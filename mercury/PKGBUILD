# Maintainer: Wesley Moore <wes@wezm.net>
# Maintainer: Frank Seifferth <frankseifferth@posteo.net>
# Contributor: Tetsumi <tetsumi@vmail.me>
# Contributor: Bart Verhoeven <bartverhoeven@live.com>

pkgname=mercury
pkgver=2024.02.27
_pkgver=2024-02-27
pkgrel=2
pkgdesc="The mercury language compiler."
arch=('i686' 'x86_64')
url="http://www.mercurylang.org/"
license=('GPL')
depends=('make')
makedepends=('flex' 'bison')
options=('staticlibs')
source=(https://github.com/Mercury-Language/mercury-srcdist/archive/rotd-${_pkgver}.tar.gz)
sha256sums=('45951f30686e06d016c7ce551409b4fcf406d0b01ac4ab898cfee34816db60cb')

build() {
  cd "$srcdir"/mercury-srcdist-rotd-$_pkgver

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --disable-csharp-grade \
              --disable-java-grade \
              --enable-libgrades=asm_fast.gc,asm_fast.gc.debug.stseg,hlc.gc,hlc.gc.memprof,hlc.gc.prof,hlc.par.gc,asm_fast.gc.profdeep.stseg

  make PARALLEL="$MAKEFLAGS"
}

package() {
  cd "$srcdir"/mercury-srcdist-rotd-$_pkgver

  make DESTDIR="$pkgdir" \
       INSTALL_PREFIX="$pkgdir"/usr \
       INSTALL_MAN_DIR="$pkgdir"/usr/share/man \
       INSTALL_INFO_DIR="$pkgdir"/usr/share/info \
       MERCURY_COMPILER="$pkgdir"/usr/bin/mercury_compile \
       MERCURY_CONFIG_DIR="$pkgdir"/usr/lib/mercury \
       PARALLEL="$MAKEFLAGS" install
}
