# Maintainer: Wesley Moore <wes@wezm.net>
# Maintainer: Frank Seifferth <frankseifferth@posteo.net>
# Contributor: Tetsumi <tetsumi@vmail.me>
# Contributor: Bart Verhoeven <bartverhoeven@live.com>

pkgname=mercury
pkgver=2022.02.26
_pkgver=2022-02-26
pkgrel=2
pkgdesc="The mercury language compiler."
arch=('i686' 'x86_64')
url="http://www.mercurylang.org/"
license=('GPL')
depends=('gcc8' 'make')
makedepends=('flex' 'bison')
options=('staticlibs')
source=(http://dl.mercurylang.org/rotd/mercury-srcdist-rotd-${_pkgver}.tar.xz)
sha256sums=('000a1dd1d0c0ab4ffe4167d1c33e42d6f9a8b04078cc28ca2ce130af5445b2d9')

build() {
  cd "$srcdir"/mercury-srcdist-rotd-$_pkgver

  CC=gcc-8 AR=gcc-ar-8 ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --disable-csharp-grade \
              --disable-java-grade \
              --enable-libgrades=asm_fast.gc,asm_fast.gc.debug.stseg,hlc.gc,hlc.gc.memprof,hlc.gc.prof,hlc.par.gc,asm_fast.gc.profdeep.stseg

  make PARALLEL="$MAKEFLAGS" CC=gcc-8 AR=gcc-ar-8
}

package() {
  cd "$srcdir"/mercury-srcdist-rotd-$_pkgver

  make DESTDIR="$pkgdir" \
       INSTALL_PREFIX="$pkgdir"/usr \
       INSTALL_MAN_DIR="$pkgdir"/usr/share/man \
       INSTALL_INFO_DIR="$pkgdir"/usr/share/info \
       MERCURY_COMPILER="$pkgdir"/usr/bin/mercury_compile \
       MERCURY_CONFIG_DIR="$pkgdir"/usr/lib/mercury \
       PARALLEL="$MAKEFLAGS" CC=gcc-8 AR=gcc-ar-8 install

  rm "${pkgdir}"/usr/bin/{mdb,mercury,mprof}.bat
}
